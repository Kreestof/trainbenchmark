apply plugin: 'groovy'

dependencies {
	compile localGroovy()
	compile project(':trainbenchmark-tool-jena')
	compile project(':trainbenchmark-tool-rdf4j')
}

sourceSets {
	main {
		groovy { srcDirs = ['src']}
	}
}

/**
 * Initialize the generator, benchmark, etc. scripts in src directory,
 * based on the template script in the src-template directory.
 */
task initScripts(type: Copy) {
	from 'src-template'
	into 'src'
	include '*.groovy'
}

/**
 * Save the current scripts from the src directory to the src-template directory.
 * Use with caution and double check the diffs before committing the changes.
 */
task saveScripts(type: Copy) {
	from 'src'
	into 'src-template'
	include '*.groovy'
}

task cleanResults(type: Delete) << {
	def resultDir = "../results"
	file(resultDir).eachDir{ dir ->
		delete "${dir}"
	}
}

task benchmark(dependsOn: 'classes', type: JavaExec) {
	main = 'BenchmarkScript'
	classpath = sourceSets.main.runtimeClasspath
}

task generate(dependsOn: 'classes', type: JavaExec) {
	main = 'GeneratorScript'
	classpath = sourceSets.main.runtimeClasspath
}

task memory(dependsOn: 'classes', type: JavaExec) {
	main = 'MemoryScript'
	classpath = sourceSets.main.runtimeClasspath
}

task qpt(dependsOn: 'classes', type: JavaExec) {
	main = 'QueryPlanTester'
	classpath = sourceSets.main.runtimeClasspath
}
